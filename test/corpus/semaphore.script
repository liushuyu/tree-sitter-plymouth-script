# Set refresh rate (frame rate) to 30 fps.
Plymouth.SetRefreshRate(30);

# Set background color to black.
Window.SetBackgroundTopColor(0, 0, 0);
Window.SetBackgroundBottomColor(0, 0, 0);

boot = [];
poweroff = [];

# Start from frame index 1 (startup-1.png, shutdown-1.png, ...).
index = 1;

fun load_sprites(stack, prefix, frames) {
    for (i = 1; i <= frames; i++) {
        stack[i].image = Image(prefix + i + ".png");
        stack[i].sprite = Sprite(stack[i].image);
        stack[i].sprite.SetOpacity(0);
        stack[i].offset_y = 1;

        # Center animation.
        stack[i].x = Window.GetX() + Window.GetWidth() / 2 - stack[i].image.GetWidth() / 2;
        stack[i].y = Window.GetX() + Window.GetHeight() * 0.625 - stack[i].image.GetHeight() / 2;
        stack[i].sprite.SetPosition(stack[i].x, stack[i].y, 0);
    }
}

fun draw_next_frame(stack) {
    for (i = 1; i <= 35; i++) {
        if (index != i) {
            stack[i].sprite.SetOpacity(0);
        }
        else {
            stack[i].sprite.SetOpacity(1);
        }
    }

    # Draw the next frame.
    # Repeat from frame 0 at frame 40: 5 empty frames until the next cycle.
    index = (index + 1) % 40;
}

# Startup animation refresh loop.
fun boot_callback() {
    draw_next_frame(boot);
}

# Shutdown/reboot animation refresh loop.
fun poweroff_callback() {
    draw_next_frame(poweroff);
}

if (Plymouth.GetMode() == "boot") {
    # Use startup loop.
    load_sprites(boot, "startup-", 35);
    Plymouth.SetRefreshFunction(boot_callback);
} else if (Plymouth.GetMode() == "shutdown" || Plymouth.GetMode() == "reboot" ) {
    # Use shutdown/reboot loop.
    load_sprites(poweroff, "shutdown-", 35);
    Plymouth.SetRefreshFunction(poweroff_callback);
}
